prog             -> classDecl funcDef progBody 
[class, ID, float, int, program]
classDecl        -> class ID { classBody } ; classDecl 
[class]
classDecl        -> EPSILON 
[ID, float, int, program]
classBody        -> type ID varOrFunc 
[ID, float, int]
classBody        -> EPSILON 
[}]
varOrFunc        -> ( fParams ) funcBody ; classBody 
[(]
varOrFunc        -> indice ; classBody 
[[, ;]
progBody         -> program funcBody ; 
[program]
funcHead         -> type ID ( fParams ) 
[ID, float, int]
funcDef          -> funcHead funcBody ; funcDef 
[ID, float, int]
funcDef          -> EPSILON 
[program]
funcBody         -> { gen_statements } 
[{]
varDecl          -> P_Type ID indice varDeclTail
[float, int]
varDecl          -> UD_Type createOrAssign
[ID]
createOrAssign   -> ID indice ; 
[ID]
createOrAssign   -> indice gen_nest assign 
[[, ., =]
gen_nest         -> . ID indice gen_nest 
[.]
gen_nest         -> EPSILON 
[=]
varDeclTail      -> is_Assign ; 
[=, ;]
is_Assign        -> assign 
[=]
is_Assign        -> EPSILON 
[;]
assign           -> assignOp expr 
[=]
statement        -> for ( type ID assignOp expr ; relExpr ; assignStat ) statBlock ; 
[for]
statement        -> if ( expr ) then statBlock else statBlock ; 
[if]
statement        -> get ( variable ) ; 
[get]
statement        -> put ( expr ) ; 
[put]
statement        -> return ( expr ) ; 
[return]
statement        -> varDecl 
[float, int, ID]
gen_statements   -> statement gen_statements 
[for, if, get, put, return, float, int, ID]
gen_statements   -> EPSILON 
[}]
assignStat       -> variable assignOp expr 
[ID]
statBlock        -> { gen_statements } 
[{]
statBlock        -> statement 
[for, if, get, put, return, float, int, ID]
statBlock        -> EPSILON 
[;, else]
expr             -> arithExpr gen_relArithExpr 
[(, FLOAT, ID, INT, not, +, -]
relExpr          -> arithExpr relOp arithExpr 
[(, FLOAT, ID, INT, not, +, -]
gen_relArithExpr -> relOp arithExpr 
[<, <=, <>, ==, >, >=]
gen_relArithExpr -> EPSILON 
[}, for, if, get, put, return, float, int, ID, ;, else, ), ,]
arithExpr        -> term gen_addArithExpr 
[(, FLOAT, ID, INT, not, +, -]
gen_addArithExpr -> addOp arithExpr 
[+, -, or]
gen_addArithExpr -> EPSILON 
[}, for, if, get, put, return, float, int, ID, ;, else, ), ,, <, <=, <>, ==, >, >=, ]]
sign             -> + 
[+]
sign             -> - 
[-]
term             -> factor gen_Term 
[(, FLOAT, ID, INT, not, +, -]
gen_Term         -> multOp term 
[*, /, and]
gen_Term         -> EPSILON 
[}, for, if, get, put, return, float, int, ID, ;, else, ), ,, <, <=, <>, ==, >, >=, ], +, -, or]
factor           -> ( arithExpr ) 
[(]
factor           -> objectVarOrFunc 
[ID]
factor           -> FLOAT 
[FLOAT]
factor           -> INT 
[INT]
factor           -> not factor 
[not]
factor           -> sign factor 
[+, -]
objectVarOrFunc  -> ID paramsOrIndice
[ID]
paramsOrIndice   -> ( aParams ) 
[(]
paramsOrIndice   -> indice is_nest
[[, EPSILON, .]
[}, for, if, get, put, return, float, int, ID, ;, else, ), ,, <, <=, <>, ==, >, >=, ], +, -, or, *, /, and]
is_nest          -> . objectVarOrFunc 
[.]
is_nest          -> EPSILON 
[}, for, if, get, put, return, float, int, ID, ;, else, ), ,, <, <=, <>, ==, >, >=, ], +, -, or, *, /, and]
variable         -> ID indice 
[ID]
indice           -> [ arithExpr ] indice 
[[]
indice           -> EPSILON 
[;, =, ., }, for, if, get, put, return, float, int, ID, else, ), ,, <, <=, <>, ==, >, >=, ], +, -, or, *, /, and]
arraySize        -> [ INT ]
[[]
arraySize        -> EPSILON 
[), ,]
type             -> ID 
[ID]
type             -> float 
[float]
type             -> int 
[int]
P_Type           -> float 
[float]
P_Type           -> int 
[int]
UD_Type          -> ID 
[ID]
fParams          -> type ID arraySize fParamsTail 
[ID, float, int]
fParams          -> EPSILON 
[)]
aParams          -> expr aParamsTail 
[(, FLOAT, ID, INT, not, +, -]
fParamsTail      -> , type ID arraySize fParamsTail 
[,]
fParamsTail      -> EPSILON 
[)]
aParamsTail      -> , expr aParamsTail 
[,]
aParamsTail      -> EPSILON 
[)]
assignOp         -> = 
[=]
relOp            -> < 
[<]
relOp            -> <= 
[<=]
relOp            -> <> 
[<>]
relOp            -> == 
[==]
relOp            -> > 
[>]
relOp            -> >= 
[>=]
addOp            -> + 
[+]
addOp            -> - 
[-]
addOp            -> or 
[or]
multOp           -> * 
[*]
multOp           -> / 
[/]
multOp           -> and 
[and]
